buildscript {
	repositories {
		// These repositories are only for Gradle plugins, put any other repositories in the repository block further below
		maven { url = 'https://maven.minecraftforge.net' }
		mavenCentral()
		maven { url = 'https://maven.parchmentmc.org' }
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.parchmentmc:librarian:1.+'
	}
}

apply plugin: 'net.minecraftforge.gradle'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = '1.0.0'
group = 'io.legom.itemupgrader'
archivesBaseName = 'itemupgrader'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
	mappings channel: 'parchment', version: '2022.07.17-1.18.2'

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			property 'forge.enabledGameTestNamespaces', 'itemupgrader'

			mods {
				itemupgrader {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			property 'forge.enabledGameTestNamespaces', 'itemupgrader'

			mods {
				itemupgrader {
					source sourceSets.main
				}
			}
		}

		gameTestServer {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			property 'forge.enabledGameTestNamespaces', 'itemupgrader'

			mods {
				itemupgrader {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			args '--mod', 'itemupgrader', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			mods {
				itemupgrader {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven {
		// location of the maven that hosts JEI files
		name = "Progwml6 maven"
		url = "https://dvs1.progwml6.com/files/maven/"
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev"
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.18.2-40.1.0'
	
	// compile against the JEI API but do not include it at runtime
	compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
	// at runtime, use the full JEI jar
	runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : "itemupgrader",
				"Specification-Vendor"    : "LegoMaster3650",
				"Specification-Version"   : "1", // We are version 1 of ourselves
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : project.jar.archiveVersion,
				"Implementation-Vendor"   : "LegoMaster3650",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

task apiJar(type: Jar) {
	archiveClassifier.set('api')
	include 'io/_3650/itemupgrader/api/**'
	from sourceSets.main.allSource
	from sourceSets.main.output
}

task sourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	archiveClassifier.set('javadoc')
	from javadoc.destinationDir
}

artifacts {
	archives apiJar
	archives sourcesJar
	archives javadocJar
}

jar.finalizedBy('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId project.archivesBaseName
			
			artifact jar
			artifact sourcesJar
			artifact apiJar
		}
	}
	repositories {
		if (project.hasProperty('mavenUrl') && project.hasProperty('mavenUser') && project.hasProperty('mavenPass')) {
			maven {
				url mavenUrl
				credentials {
					username mavenUser
					password mavenPass
				}
			}
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}
